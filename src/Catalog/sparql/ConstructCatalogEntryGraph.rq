PREFIX nuget: <http://schema.nuget.org/schema#>
PREFIX catalog: <http://schema.nuget.org/catalog#>

CONSTRUCT
{
    ?catalogEntry a nuget:PackageDetails ;
                  nuget:id ?id ;
                  nuget:version ?version ;
	              nuget:published ?published ;
                  nuget:packageContent ?packageContent ;
                  nuget:dependencyGroup ?dependency_group ;
                  nuget:listed ?listed ;
                  nuget:description ?description ;
                  nuget:title ?title ;
                  nuget:summary ?summary ;
                  nuget:iconUrl ?iconUrl ;
                  nuget:licenseUrl ?licenseUrl ;
                  nuget:licenseExpression ?licenseExpression ;
                  nuget:projectUrl ?projectUrl ;
                  nuget:requireLicenseAcceptance ?requireLicenseAcceptance ;
                  nuget:language ?language ;
                  nuget:authors ?authors ;
                  nuget:tag ?tag ;
                  nuget:minClientVersion ?minClientVersion ;
                  nuget:deprecation ?deprecation .

    ?dependency_group a nuget:PackageDependencyGroup ;
	                  nuget:dependency ?dependency ;
                      nuget:targetFramework ?dependency_group_targetFramework .

    ?dependency a nuget:PackageDependency ;
	            nuget:id ?dependency_id ;
                nuget:range ?dependency_range ;
                nuget:version ?dependency_version .

    ?deprecation a nuget:deprecation ;
                 nuget:reasons ?deprecation_reasons ;
                 nuget:message ?deprecation_message ;
                 nuget:alternatePackage ?deprecation_alternatePackage .

    ?deprecation_alternatePackage a nuget:alternatePackage ;
                                  nuget:id ?deprecation_alternatePackage_id ;
                                  nuget:range ?deprecation_alternatePackage_range .
}
WHERE
{
    BIND (@catalogEntry AS ?catalogEntry)

    ?catalogEntry nuget:version ?version ;
                  nuget:id ?id ;
	              nuget:published ?published ;

	OPTIONAL { ?catalogEntry nuget:packageContent ?packageContent . }
	OPTIONAL { ?catalogEntry nuget:listed ?listed . }
    OPTIONAL { ?catalogEntry nuget:description ?description . }
    OPTIONAL { ?catalogEntry nuget:title ?title . }
    OPTIONAL { ?catalogEntry nuget:summary ?summary . }
    OPTIONAL { ?catalogEntry nuget:iconUrl ?iconUrl . }
    OPTIONAL { ?catalogEntry nuget:licenseUrl ?licenseUrl . }
    OPTIONAL { ?catalogEntry nuget:licenseExpression ?licenseExpression . }
    OPTIONAL { ?catalogEntry nuget:projectUrl ?projectUrl . }
    OPTIONAL { ?catalogEntry nuget:requireLicenseAcceptance ?requireLicenseAcceptance . }
    OPTIONAL { ?catalogEntry nuget:language ?language . }
    OPTIONAL { ?catalogEntry nuget:authors ?authors . }
    OPTIONAL { ?catalogEntry nuget:tag ?tag . }
    OPTIONAL { ?catalogEntry nuget:minClientVersion ?minClientVersion . }

    OPTIONAL
    {
        ?catalogEntry nuget:dependencyGroup ?dependency_group .
		OPTIONAL { ?dependency_group nuget:targetFramework ?dependency_group_targetFramework . }
		OPTIONAL
		{
			?dependency_group nuget:dependency ?dependency .
			?dependency nuget:id ?dependency_id .
			OPTIONAL { ?dependency nuget:range ?dependency_range . }
			OPTIONAL { ?dependency nuget:version ?dependency_version . }
		}
    }
    
    OPTIONAL
    {
        ?catalogEntry nuget:deprecation ?deprecation .
        ?deprecation nuget:reasons ?deprecation_reasons .
        OPTIONAL { ?deprecation nuget:message ?deprecation_message . }
        OPTIONAL 
        { 
            ?deprecation nuget:alternatePackage ?deprecation_alternatePackage . 
            ?deprecation_alternatePackage nuget:id ?deprecation_alternatePackage_id .
            ?deprecation_alternatePackage nuget:range ?deprecation_alternatePackage_range .
        }
    }
}
